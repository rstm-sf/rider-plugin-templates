buildscript {
    repositories {
        maven { setUrl("https://cache-redirector.jetbrains.com/www.myget.org/F/rd-snapshots/maven") }
        mavenCentral()
    }

    // https://www.myget.org/feed/rd-snapshots/package/maven/com.jetbrains.rd/rd-gen
    dependencies { classpath "com.jetbrains.rd:rd-gen:0.202.113" }
}

plugins {
    id 'java'
    id 'me.filippov.gradle.jvm.wrapper' version '0.9.3'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.intellij' version '0.4.16'
}

dependencies {
    testImplementation 'org.testng:testng:6.14.3'
}

ext {
    pluginVersionBase = '0.0.1'
    sdkVersion = '2020.3-SNAPSHOT'
    rdLibDirectory = {
        new File(intellij.ideaDependency.classes, "lib/rd")
    }
    dotNetDir = new File(projectDir, "src/main/csharp")
    dotNetPluginId = "RiderPluginExample"
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '6.7.1'
    distributionType = Wrapper.DistributionType.ALL
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

jvmWrapper {
    // https://confluence.jetbrains.com/display/JBR/Release+notes
    linuxJvmUrl = 'https://bintray.com/jetbrains/intellij-jbr/download_file?file_path=jbrsdk-11_0_9_1-linux-x64-b1145.63.tar.gz'
    macJvmUrl = 'https://bintray.com/jetbrains/intellij-jbr/download_file?file_path=jbrsdk-11_0_9_1-osx-x64-b1145.63.tar.gz'
    windowsJvmUrl = 'https://bintray.com/jetbrains/intellij-jbr/download_file?file_path=jbrsdk-11_0_9_1-windows-x64-b1145.63.tar.gz'
}

if (!ext.has("BuildConfiguration"))
    ext.BuildConfiguration = "Debug"

if (ext.has('buildNumber'))
    ext.version = "$ext.pluginVersionBase.$ext.buildNumber"
else
    ext.version = "$ext.pluginVersionBase.0"

version ext.version

sourceSets {
    main {
        java.srcDir 'src/main/kotlin'
        resources.srcDir 'src/main/resources'
    }
}

compileKotlin {
    dependsOn 'rdgen'
    kotlinOptions { jvmTarget = "1.8" }
}

task compileDotNet {
    dependsOn 'rdgen'
    doLast {
        exec {
            executable "dotnet"
            args "build", "-c", BuildConfiguration
        }
    }
}

buildPlugin {
    dependsOn compileDotNet
}

intellij {
    type = 'RD'
    version = "$sdkVersion"
    downloadSources = false
}

test {
    useTestNG()
}

apply plugin: 'com.jetbrains.rdgen'

rdgen {
    def modelDir = new File(projectDir, "protocol/src/main/kotlin/model")
    def csOutput = new File(projectDir, "src/main/csharp/RiderPluginExample/Model")
    def ktOutput = new File(projectDir, "src/main/kotlin/model")

    verbose = true
    classpath {
        "${rdLibDirectory()}/rider-model.jar"
    }
    sources "$modelDir"
    hashFolder = "$rootDir/build/rdgen/rider"
    packages = "model"

    generator {
        language = "kotlin"
        transform = "asis"
        root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
        namespace = "com.jetbrains.rider.model"
        directory = "$ktOutput"
    }

    generator {
        language = "csharp"
        transform = "reversed"
        root = "com.jetbrains.rider.model.nova.ide.IdeRoot"
        namespace = "JetBrains.Rider.Model"
        directory = "$csOutput"
    }
}

patchPluginXml {
    sinceBuild = '203.6682' // 2020.3
    untilBuild = null

    def changelogText = file("$projectDir/CHANGELOG.md").text
    def changelogMatches = changelogText =~ /(?s)##(.+?)(.+?)(?=##|$)/

    changeNotes = changelogMatches.collect {
        def versionTitle = it[1]
        def versionText = it[2].replaceAll(/(?s)\r?\n/, "<br />\n")
        "<b>$versionTitle</b>$versionText"
    }.take(10).join('')
}

prepareSandbox {
    def outputFolder = "$dotNetDir/$dotNetPluginId/bin/${dotNetPluginId}/$BuildConfiguration"
    def dllFiles = [
        "$outputFolder/${dotNetPluginId}.dll",
        "$outputFolder/${dotNetPluginId}.pdb"
    ]

    dllFiles.forEach({ f ->
        def file = file(f)
        from(file, { into "$intellij.pluginName/dotnet" })
    })

    doLast {
        dllFiles.forEach({ f ->
            def file = file(f)
            if (!file.exists()) throw new RuntimeException("File $file does not exist")
        })
    }
}

buildSearchableOptions.enabled = false
